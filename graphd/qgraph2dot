#!/usr/bin/env coffee
# qgraph2dot -- a tool for visualizing the query graph used in state machine compilation
# Author: Jaeho Shin <netj@cs.stanford.edu>
# Created: 2012-02-15

fs = require "fs"
echo = console.log

statemachine = JSON.parse fs.readFileSync (arguments[0] ? "/dev/stdin")

qgraph = statemachine.qgraph

echo "digraph {"

# nodes
for n in qgraph.nodes
    echo "s#{n.id} ["
    echo "  label=\"#{
        if n.name?
            "$#{n.name} = "
        else
            ""
    }#{n.step.objectType}"
    if n.isInitial
        echo "  color=\"#33cc33\", style=bold"
    if n.isTerminal
        echo "  color=\"#ff3333\", shape=doubleoctagon, style=\"bold\""
    echo " ];"

# edges
for e in qgraph.edges
    echo "s#{e.source} -> s#{e.target} ["
    echo "  label=\"#{
        if e.msgIdArrived?
            "#{e.msgIdArrived}=#{e.msgIdWalkingBase}..#{
                e.msgIdWalkingBase+e.steps.length-2} (w#{e.id})"
        else if e.msgIdReturned?
            "#{e.msgIdReturned} (w#{e.walk})"
    }\""
    if e.steps?
        echo "  color=\"#3366ff\", fontcolor=\"#003366\""
    else if e.walk?
        echo "  style=dashed, color=\"#cccccc\", fontcolor=\"#999999\""
    echo " ];"

echo "};"

# vim:ft=coffee
